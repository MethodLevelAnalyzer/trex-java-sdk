group 'com.cisco.trex'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'com.github.sherter.google-java-format'

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "gradle.plugin.com.github.sherter.google-java-format:google-java-format-gradle-plugin:0.8"
  }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.pcap4j', name: 'pcap4j-packetfactory-static', version:'1.7.7' //1.8.0+ requires java 9
    compile group: 'org.apache.commons', name: 'commons-lang3', version:'3.9'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.11.2'
    compile group: 'org.zeromq', name: 'jeromq', version:'0.5.1'
    compile group: 'com.google.code.gson', name: 'gson', version:'2.8.5'
    compile group: 'com.google.guava', name: 'guava', version: '24.0-jre'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.9.8'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.28.2'

    sourceSets {
        e2eTest {
            compileClasspath = sourceSets.main.output + configurations.testRuntime
            runtimeClasspath = output + sourceSets.main.output + configurations.testRuntime
            java {
                srcDirs = ["src/e2e-test/java"]
            }
        }
    }
}

task writePom {
    doLast {
        pom {
            project {
                name 'TRex Java Sdk'
                packaging 'jar'
            	artifactId 'trex-java-sdk'
            	description 'Java client SDK provides an implementation for TRex RPC APIs'
                url 'https://github.com/cisco-system-traffic-generator/trex-java-sdk'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                
                scm {
		          connection 'scm:git:git://github.com/cisco-system-traffic-generator/trex-java-sdk.git'
		          developerConnection 'scm:git:ssh://github.com:cisco-system-traffic-generator/trex-java-sdk.git'
		          url 'https://github.com/cisco-system-traffic-generator/trex-java-sdk'
		        }
		        
		        developers {
		          developer {
		            name 'Leo Ma'
		            email 'leo.ma@ericsson.com'
		          }
		          developer {
		            name 'Emil Gustafsson'
		            email 'emil.gustafsson@ericsson.com'
		          }
        		}
            }
        }.writeTo("pom.xml")
    }
}

jar {
    dependsOn writePom
    into("META-INF/maven/$project.group/$project.name") {
	from("pom.xml")
    }
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	javadoc.options.addStringOption('Xdoclint:none', '-quiet')
	from javadoc
}

task sourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task jarWithDependencies(type: Jar) {
    dependsOn writePom
    baseName = project.name + '-with-dependencies'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

artifacts {
  archives javadocJar, sourcesJar, jarWithDependencies
}

test {
    testLogging {
        events "passed", "skipped" , "failed"
    }
}

task e2eTest(type: Test) {
    group 'verification'
    description 'Runs E2E testing on trex-java-sdk'

    outputs.upToDateWhen { false }
    testClassesDirs = sourceSets.e2eTest.output.classesDirs
    classpath = sourceSets.e2eTest.runtimeClasspath

    test {
        testLogging {
            events "passed", "skipped" , "failed"
            exceptionFormat "full"
        }
    }
}
